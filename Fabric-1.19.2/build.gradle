plugins {
	id "fabric-loom" version "1.0.17"
}

archivesBaseName = "${rootProject.name}-fabric-1.19.2"

dependencies {
	//Minecraft
	minecraft "com.mojang:minecraft:1.19.2"
	mappings loom.officialMojangMappings()
	
	//Fabric loader
	modImplementation "net.fabricmc:fabric-loader:0.14.9"
	
	//Fabric API
	modImplementation fabricApi.module("fabric-resource-loader-v0", "0.59.0+1.19.2")
	modImplementation fabricApi.module("fabric-lifecycle-events-v1", "0.59.0+1.19.2")
	modImplementation fabricApi.module("fabric-command-api-v2", "0.59.0+1.19.2")
	
	//Xplat
	implementation project(":Xplat")
}

//fold in xplat resources and sources
processResources {
	from project(":Xplat").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
	source(project(":Xplat").sourceSets.main.allSource)
}

//loom doesn't generate run configs by default in subprojects
loom {
	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("./run")
		}
		server {
			server()
			setConfigName("Fabric Server")
			ideConfigGenerated(true)
			runDir("./run")
		}
	}
	
	//nor can it figure out the refmap name in this situation
	mixin {
		//you must go through project.rootProject due to a goofy name conflict with LoomGradleExtensionImpl#isRootProject
		defaultRefmapName = "${project.rootProject.name}.refmap.json"
	}
}