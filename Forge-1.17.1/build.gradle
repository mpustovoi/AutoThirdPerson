buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

ext.gimmeJava(16)

archivesBaseName = "${rootProject.name}-forge-1.17.1"

mixin {
	add sourceSets.main, "${rootProject.name}.refmap.json"
	
	config "${rootProject.name}.mixins.json"
}

minecraft {
	mappings channel: 'official', version: "1.17.1"
	
	runs {
		client {
			workingDirectory file('./run')
			property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			ideaModule "auto_third_person.Forge-1.17.1.main"
			mods {
				"${rootProject.name}" {
					source sourceSets.main
					source project(":Xplat-1.17.1").sourceSets.main
					source project(":Core").sourceSets.main
				}
			}
		}
		
		server {
			workingDirectory file('./run')
			property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			ideaModule "auto_third_person.Forge-1.17.1.main"
			mods {
				"${rootProject.name}" {
					source sourceSets.main
					source project(":Xplat-1.17.1").sourceSets.main
					source project(":Core").sourceSets.main
				}
			}
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:1.17.1-37.1.1"
	annotationProcessor "org.spongepowered:mixin:0.8.4-SNAPSHOT:processor" //i imagine mixingradle does this but idk
	
	implementation project(":Xplat-1.17.1")
	implementation project(":Core")
}

//fold in xplat resources and sources
processResources {
	from project(":Xplat-1.17.1").sourceSets.main.resources
	from project(":Core").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
	source(project(":Xplat-1.17.1").sourceSets.main.allSource)
	source(project(":Core").sourceSets.main.allSource)
}