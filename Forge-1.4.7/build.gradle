buildscript {
	repositories {
		mavenCentral()
		maven { url "https://maven.fabricmc.net" }
		maven { url "https://repo.sleeping.town" }
	}
	dependencies {
		classpath "agency.highlysuspect:voldeloom:2.3"
	}
}

apply plugin: "agency.highlysuspect.voldeloom"

archivesBaseName = "${rootProject.name}-forge-1.4.7"

ext.gimmeJava(6)

volde {
	runs {
		client {
			//TODO: voldeloom issue #1 (lmao)
			runDir = "Forge-1.4.7/run"
		}
	}
}

String minecraftVersion = "1.4.7"
String forgeVersion = "1.4.7-6.6.2.534"

dependencies {
	minecraft "com.mojang:minecraft:${minecraftVersion}"
	forge "net.minecraftforge:forge:${forgeVersion}:universal@zip"
	//mappings "net.minecraftforge:forge:${forgeVersion}:src@zip"
	
	//VOLDELOOM BUG that I am very surprised with!!
	//This class was showing up in net/minecraft/src/Block$1 which makes me think packaging xform wasn't working correctly
	mappings volde.layered {
		importBaseZip("net.minecraftforge:forge:${forgeVersion}:src@zip")
		modify("kludge-fix", builder -> {
			builder.joined.putClassMapping("amq\$1", "net/minecraft/block/Block\$1")
		})
	}
	
	implementation project(":Core")
}

//fold in xplat resources and sources
processResources {
	from project(":Core").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
	source(project(":Core").sourceSets.main.allSource)
}